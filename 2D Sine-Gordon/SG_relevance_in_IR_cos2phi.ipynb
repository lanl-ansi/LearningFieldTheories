{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "88bb9c8c",
   "metadata": {},
   "source": [
    "We consider a term of the form $u\\text{cos}(2\\beta\\phi)$ to see if it is relevant in the IR. We start by generating samples, specifying the RG scheme, and then re learning after coarse-graining."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "522e6c7d",
   "metadata": {},
   "outputs": [],
   "source": [
    "using AdvancedHMC, ForwardDiff\n",
    "using LogDensityProblems, LinearAlgebra, Random, Statistics, Optim, GalacticOptim, Plots\n",
    "\n",
    "# 2D Sine-Gordon Model Definition\n",
    "struct SineGordonModel2D\n",
    "    lattice_size::Int   # Lattice size (NxN)\n",
    "    α::Float64          # Coupling strength for the sine term\n",
    "    β::Float64          # Mass parameter (m)\n",
    "    u :: Float64        #coeff of cos 2betaphi\n",
    "end\n",
    "\n",
    "# Total energy (kinetic + gradient + sine potential)\n",
    "function calculate_energy_2d(phi::Matrix{T}, model::SineGordonModel2D) where T<:Real\n",
    "    N = size(phi,1)\n",
    "    kin = zero(T)\n",
    "    grad = zero(T)\n",
    "    sine = zero(T)\n",
    "    α, β, u = model.α, model.β, model.u\n",
    "\n",
    "    for i in 1:N, j in 1:N\n",
    "        ip = mod1(i+1, N); im = mod1(i-1, N)\n",
    "        jp = mod1(j+1, N); jm = mod1(j-1, N)\n",
    "        # Kinetic term: discrete gradient\n",
    "        kin  += 0.5 * ((phi[i,j] - phi[ip,j])^2 + (phi[i,j] - phi[i,jp])^2)\n",
    "        # Sine interaction potential\n",
    "        sine += (α/β^2) * (1 - cos(β * phi[i,j])) + u * cos(2*β*phi[i,j])\n",
    "    end\n",
    "    return kin + sine\n",
    "end\n",
    "\n",
    "# HMC log-density wrapper\n",
    "struct SGTarget2D\n",
    "    model::SineGordonModel2D\n",
    "end\n",
    "\n",
    "function LogDensityProblems.logdensity(p::SGTarget2D, x::Vector{T}) where T<:Real\n",
    "    N = p.model.lattice_size\n",
    "    phi = reshape(x, N, N)\n",
    "    return -calculate_energy_2d(phi, p.model)\n",
    "end\n",
    "\n",
    "LogDensityProblems.dimension(p::SGTarget2D) = p.model.lattice_size^2\n",
    "LogDensityProblems.capabilities(::Type{SGTarget2D}) = LogDensityProblems.LogDensityOrder{1}()\n",
    "\n",
    "# HMC data generation function\n",
    "function generate_sg_data_hmc_2d(model::SineGordonModel2D, n_samples::Int; n_adapts::Int=2000)\n",
    "    N = model.lattice_size\n",
    "    target     = SGTarget2D(model)\n",
    "    init_field = randn(N, N)\n",
    "\n",
    "    metric     = DiagEuclideanMetric(N^2)\n",
    "    ham        = Hamiltonian(metric, target, ForwardDiff)\n",
    "    ε          = find_good_stepsize(ham, vec(init_field))\n",
    "    integrator = Leapfrog(ε)\n",
    "    kernel     = HMCKernel(Trajectory{MultinomialTS}(integrator, GeneralisedNoUTurn()))\n",
    "    adaptor    = StanHMCAdaptor(MassMatrixAdaptor(metric), StepSizeAdaptor(0.8, integrator))\n",
    "\n",
    "    samples_vec, _ = sample(ham, kernel, vec(init_field), n_samples, adaptor, n_adapts; progress=false)\n",
    "    return [reshape(s, N, N) for s in samples_vec]\n",
    "end\n",
    "\n",
    "# Score matching objective (no spacing parameter)\n",
    "function score_matching_objective_2d(α::Float64, β::Float64, u::Float64, data::Vector{Matrix{Float64}})\n",
    "    N = size(data[1],1)\n",
    "    total_obj = 0.0\n",
    "    for φ in data\n",
    "        for i in 1:N, j in 1:N\n",
    "            x = φ[i,j]\n",
    "            # Periodic neighbors\n",
    "            xL = φ[mod1(i-1,N), j]; xR = φ[mod1(i+1,N), j]\n",
    "            xD = φ[i, mod1(j-1,N)]; xU = φ[i, mod1(j+1,N)]\n",
    "            lap = 4*x - (xL + xR + xD + xU)\n",
    "\n",
    "            # Score and its derivative\n",
    "            score = -(lap + (α/β) * sin(β * x) - 2*u*β*sin(2*β*x))\n",
    "            ds    = -(4 + α * cos(β * x) - 4 * u * β^2 * cos(2 * β * x))\n",
    "\n",
    "            total_obj += 0.5 * score^2 + ds\n",
    "        end\n",
    "    end\n",
    "    return total_obj / length(data)\n",
    "end\n",
    "\n",
    "# Estimate α, β by Particle Swarm optimization\n",
    "function estimate_parameters_score_matching_2d(data::Vector{Matrix{Float64}})\n",
    "    objective(x) = score_matching_objective_2d(x[1], x[2], x[3],  data)\n",
    "    initial = [0.1, 1.0, 0.5]\n",
    "    lower   = [0.01, 0.01, 0.01]; upper = [10.0, 10.0, 10.0]\n",
    "    ps      = ParticleSwarm(lower = lower, upper = upper)\n",
    "    result  = optimize(objective, initial, ps)\n",
    "    return result.minimizer[1], result.minimizer[2], result.minimizer[3]\n",
    "end\n",
    "\n",
    "# Compute raw gradient squared measure\n",
    "function compute_raw_grad_squared(phi::Matrix{Float64})\n",
    "    gradx = circshift(phi, (-1, 0)) .- phi\n",
    "    grady = circshift(phi, (0, -1)) .- phi\n",
    "    return mean(gradx.^2 .+ grady.^2)\n",
    "end\n",
    "\n",
    "# Block-spin coarse-graining with kinetic renormalization\n",
    "function block_spin_transform(phi::Matrix{Float64})\n",
    "    nx, ny = size(phi)\n",
    "    @assert nx % 2 == 0 && ny % 2 == 0\n",
    "    nx2, ny2 = nx÷2, ny÷2\n",
    "    return [ median(vec(phi[2i-1:2i, 2j-1:2j])) \n",
    "             for i in 1:nx2, j in 1:ny2 ]\n",
    "end\n",
    "\n",
    "block_spin_samples(data::Vector{Matrix{Float64}}) = [ block_spin_transform(φ) for φ in data ]\n",
    "# RG flow loop\n",
    "function run_rg_procedure(model::SineGordonModel2D, n_samples::Int, n_steps::Int, reps::Int)\n",
    "    alphas = Float64[]; betas = Float64[]; us = Float64[]; sizes = Int[]\n",
    "    push!(alphas, model.α); push!(betas, model.β); push!(us, model.u); push!(sizes, model.lattice_size)\n",
    "\n",
    "    samples = generate_sg_data_hmc_2d(model, n_samples)\n",
    "    for step in 1:n_steps\n",
    "        samples = block_spin_samples(samples)\n",
    "        new_size = size(samples[1],1)\n",
    "\n",
    "        αs = Float64[]; βs = Float64[]; vs = Float64[]\n",
    "        for rep in 1:reps\n",
    "            α̂, β̂,u = estimate_parameters_score_matching_2d(samples)\n",
    "            push!(αs, α̂); push!(βs, β̂); push!(vs,u)\n",
    "        end\n",
    "\n",
    "        push!(alphas, mean(αs)); push!(betas, mean(βs)); push!(us, mean(vs)); push!(sizes, new_size)\n",
    "    end\n",
    "    return alphas, betas, us, sizes\n",
    "end\n",
    "\n",
    "# Example usage\n",
    "model0 = SineGordonModel2D(32, 3.0, sqrt(8π), 1.5)\n",
    "println(\"\\nVerifying parameter recovery on original lattice:\")\n",
    "samples0 = generate_sg_data_hmc_2d(model0, 10000)\n",
    "α_est0, β_est0, u_est0 = estimate_parameters_score_matching_2d(samples0)\n",
    "println(\"True α = \", model0.α, \", Estimated α = \", α_est0)\n",
    "println(\"True β = \", model0.β, \", Estimated β = \", β_est0)\n",
    "println(\"True u = \", model0.u, \", Estimated u = \", u_est0)\n",
    "\n",
    "alphas, betas, us, sizes = run_rg_procedure(model0, 10000, 3, 2)\n",
    "\n",
    "p = plot(sizes, alphas; label=\"α\", marker=:o, lw=2,\n",
    "         xlabel=\"Lattice size\", ylabel=\"Parameter\", xscale=:log2)\n",
    "plot!(sizes, betas; label=\"β\", marker=:s, lw=2)\n",
    "plot!(sizes, us; label = \"u\", marker = :square, lw = 2)\n",
    "display(p)\n",
    "savefig(p, \"sg_rg_flow_cos2phi.png\")\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4a14e170",
   "metadata": {},
   "source": [
    "As usual, we perform RG separately, where parameters after RG are named as {parameter}_{RG step}.\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0367ebf6",
   "metadata": {},
   "outputs": [],
   "source": [
    "samples1 = block_spin_samples(samples0)\n",
    "α_est1, β_est1, u_est1 = estimate_parameters_score_matching_2d(samples1)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e3b7ad5c",
   "metadata": {},
   "outputs": [],
   "source": [
    "samples2 = block_spin_samples(samples1)\n",
    "α_est2, β_est2, u_est2 = estimate_parameters_score_matching_2d(samples2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "f64665b6",
   "metadata": {},
   "outputs": [],
   "source": [
    "samples3 = block_spin_samples(samples2)\n",
    "α_est3, β_est3, u_est3 = estimate_parameters_score_matching_2d(samples3)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.4",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.4"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
